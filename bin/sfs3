#!/usr/bin/perl

use strict;
use warnings;
use feature qw( say switch );

use Socialflow::S3;

use IO::Async::Loop;

use File::Basename qw( basename );
use Getopt::Long;
use YAML qw( LoadFile );

Getopt::Long::Configure( "bundling" );

my $config = LoadFile( "etc/sfs3.conf" );

my $loop = IO::Async::Loop->new;
my $s3 = Socialflow::S3->new(
   access_key => $config->{access_key},
   secret_key => $config->{secret_key},
   bucket     => $config->{bucket},
   ssl        => $config->{ssl},
   crypto     => $config->{crypto},
   crypto_passphrase => $config->{crypto_passphrase},
);
$loop->add( $s3 );

sub do_help
{
   print STDERR <<'EOF';

Usage: sfs3 COMMAND [OPTIONS...]

Commands:
   ls [--long] [--recursive] S3PATH
   cat S3FILE
   uncat S3FILE
   get S3FILE [LOCALFILE]
   put LOCALFILE [S3FILE]
   rm S3PATTERN

   push LOCALROOT S3ROOT
     [--concurrent=N] [--all] [--md5sum] [--only=PATTERN]* [--exclude=PATTERN]*

   pull S3ROOT LOCALROOT
     [--concurrent=N] [--all] [--md5sum] [--only=PATTERN]* [--exclude=PATTERN]*

Patterns:
  May contain wildcards
    ?  A single character, not '/'
    *  Zero or more characters, not '/'
    ** Zero or more characters, including '/'

EOF
}

## main
given( shift @ARGV ) {
   when([ undef, "help" ]) {
      do_help;
      exit 0;
   }

   when( "ls" ) {
      GetOptions(
         'l|long' => \my $LONG,
         'r|recursive' => \my $RECURSE,
      ) or exit 1;
      my $s3path = shift @ARGV;

      $s3->cmd_ls( $s3path, long => $LONG, recurse => $RECURSE );
   }
   when( "cat" ) {
      my $s3path = shift @ARGV;

      $s3->cmd_cat( $s3path );
   }
   when( "uncat" ) {
      my $s3path = shift @ARGV;

      $s3->cmd_uncat( $s3path );
   }
   when( "get" ) {
      my $s3path = shift @ARGV;
      my $localpath = shift @ARGV // basename( $s3path );

      $s3->cmd_get( $s3path, $localpath );
   }
   when( "put" ) {
      my $localpath = shift @ARGV;
      my $s3path = shift @ARGV;
      if( !defined $s3path ) {
         $s3path = basename( $localpath );
      }
      elsif( $s3path =~ m{/$} ) {
         $s3path .= basename( $localpath );
      }

      $s3->cmd_put( $localpath, $s3path );
   }
   when( "rm" ) {
      my $s3path = shift @ARGV;

      $s3->cmd_rm( $s3path );
   }
   when( "push" ) {
      my $SKIP_LOGIC;
      GetOptions(
         'c|concurrent=i' => \my $CONCURRENT,
         'a|all'          => sub { $SKIP_LOGIC = "all" },
         '5|md5sum'       => sub { $SKIP_LOGIC = "md5sum" },
         'only=s'         => \my @ONLYS,
         'exclude=s'      => \my @EXCLUDES,
      ) or exit 1;
      my $localroot = shift @ARGV;
      my $s3root = shift @ARGV // $localroot;

      $s3->cmd_push( $localroot, $s3root,
         concurrent => $CONCURRENT,
         skip_logic => $SKIP_LOGIC,
         only => \@ONLYS, exclude => \@EXCLUDES,
      );
   }
   when( "pull" ) {
      my $SKIP_LOGIC;
      GetOptions(
         'c|concurrent=i' => \my $CONCURRENT,
         'a|all'          => sub { $SKIP_LOGIC = "all" },
         '5|md5sum'       => sub { $SKIP_LOGIC = "md5sum" },
         'only=s'         => \my @ONLYS,
         'exclude=s'      => \my @EXCLUDES,
      ) or exit 1;
      my $s3root = shift @ARGV;
      my $localroot = shift @ARGV // $s3root;

      $s3->cmd_pull( $s3root, $localroot,
         concurrent => $CONCURRENT,
         skip_logic => $SKIP_LOGIC,
         only => \@ONLYS, exclude => \@EXCLUDES,
      );
   }

   # ll for "low-level" commands; access directly onto the base "filesystem"
   # of S3 rather than data/metadata layer on top. Useful for debugging and
   # development
   when( "ll-ls" ) {
      my $s3path = shift @ARGV;
      my ( $keys, $prefixes ) = $s3->{s3}->list_bucket(
         prefix => $s3path, delimiter => "/",
      )->get;
      print "$_->{key}\n" for @$keys;
      print "$_\n" for @$prefixes;
   }
   when( "ll-cat" ) {
      my $s3path = shift @ARGV;
      print scalar $s3->{s3}->get_object(
         key => $s3path,
      )->get;
   }
   when( "ll-uncat" ) {
      my $s3path = shift @ARGV;
      $s3->{s3}->put_object(
         key => $s3path,
         value => do { local $/; scalar <STDIN> },
      )->get;
   }
   when( "ll-rm" ) {
      my $s3path = shift @ARGV;
      $s3->{s3}->delete_object(
         key => $s3path,
      )->get;
   }

   default {
      print STDERR "Unrecognised subcommand '$_'\n";
      do_help;
      exit 1;
   }
}
