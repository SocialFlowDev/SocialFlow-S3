#!/usr/bin/perl

use strict;
use warnings;
use feature qw( say switch );

use YAML qw( LoadFile );
use Net::Async::AmazonS3::FSLike;
use IO::Async::Loop;
use Future;
use Getopt::Long;

my $config = LoadFile( "etc/sfs3.conf" );

my $loop = IO::Async::Loop->new;
my $s3 = Net::Async::AmazonS3::FSLike->new(
   access_key => $config->{access_key},
   secret_key => $config->{secret_key},
);
$loop->add( $s3 );

## main
given( shift @ARGV ) {
   when( "ls" ) {
      GetOptions(
         'l|long' => \my $LONG,
      );
      my $s3path = shift @ARGV;
      my @content = $s3->list_dir(
         bucket => $config->{bucket},
         path   => $s3path,
      )->get;

      foreach my $e ( @content ) {
         if( $LONG and $e->{type} eq "F" ) {
            printf "%s %-36s %15d %s\n", $e->{type}, $e->{name}, $e->{size}, $e->{last_modified};
         }
         else {
            printf "%s %s\n", $e->{type}, $e->{name};
         }
      }
   }
   when( "cat" ) {
      my $s3path = shift @ARGV;
      $s3->get_file(
         bucket => $config->{bucket},
         path   => $s3path,
         on_chunk => sub {
            my ( $header, $chunk ) = @_;
            print $chunk;
         },
      )->get;
   }
   when( "get" ) {
      my $s3path = shift @ARGV;
      my $localpath = shift @ARGV // $s3path;

      my ( $fh, $len_so_far, $len_total );
      $s3->get_file(
         bucket => $config->{bucket},
         path   => $s3path,
         on_chunk => sub {
            my ( $header, $chunk ) = @_;

            if( !$fh ) {
               open $fh, ">", $localpath or die "Cannot write $localpath - $!";
               $len_so_far = 0;
               $len_total = $header->content_length;
            }

            $fh->print( $chunk );
            $len_so_far += length $chunk;

            # TODO: This needs to be much better
            printf "Done %.2f%% (%d of %d)\n", 100 * $len_so_far / $len_total, $len_so_far, $len_total;
         },
      )->get;
   }
}
