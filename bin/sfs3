#!/usr/bin/perl

use strict;
use warnings;
use feature qw( say switch );

use Socialflow::S3;

use IO::Async::Loop;

use File::Basename qw( basename );
use Getopt::Long;
use YAML qw( LoadFile );

my $config = LoadFile( "etc/sfs3.conf" );

my $loop = IO::Async::Loop->new;
my $s3 = Socialflow::S3->new(
   access_key => $config->{access_key},
   secret_key => $config->{secret_key},
   bucket     => $config->{bucket},
);
$loop->add( $s3 );

## main
given( shift @ARGV ) {
   when( "ls" ) {
      GetOptions(
         'l|long' => \my $LONG,
      );
      my $s3path = shift @ARGV;

      $s3->cmd_ls( $s3path, long => $LONG );
   }
   when( "cat" ) {
      my $s3path = shift @ARGV;

      $s3->cmd_cat( $s3path );
   }
   when( "get" ) {
      my $s3path = shift @ARGV;
      my $localpath = shift @ARGV // basename( $s3path );

      $s3->cmd_get( $s3path, $localpath );
   }
   when( "put" ) {
      my $localpath = shift @ARGV;
      my $s3path = shift @ARGV;
      if( !defined $s3path ) {
         $s3path = basename( $localpath );
      }
      elsif( $s3path =~ m{/$} ) {
         $s3path .= basename( $localpath );
      }

      $s3->cmd_put( $localpath, $s3path );
   }
   when( "rm" ) {
      my $s3path = shift @ARGV;

      $s3->cmd_rm( $s3path );
   }

   # ll for "low-level" commands; access directly onto the base "filesystem"
   # of S3 rather than data/metadata layer on top. Useful for debugging and
   # development
   when( "ll-ls" ) {
      my $s3path = shift @ARGV;
      my ( $keys, $prefixes ) = $s3->{s3}->list_bucket(
         prefix => $s3path, delimiter => "/",
      )->get;
      print "$_->{key}\n" for @$keys;
      print "$_\n" for @$prefixes;
   }
   when( "ll-cat" ) {
      my $s3path = shift @ARGV;
      print scalar $s3->{s3}->get_object(
         key => $s3path,
      )->get;
   }
   when( "ll-uncat" ) {
      my $s3path = shift @ARGV;
      $s3->{s3}->put_object(
         key => $s3path,
         value => do { local $/; scalar <STDIN> },
      )->get;
   }
   when( "ll-rm" ) {
      my $s3path = shift @ARGV;
      $s3->{s3}->delete_object(
         key => $s3path,
      )->get;
   }
}
